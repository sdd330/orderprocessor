buildscript {
	ext {
		springBootVersion = '1.4.0.BUILD-SNAPSHOT'
		//springBootVersion = '1.3.4.RELEASE'
	}
	repositories {
		mavenCentral()
		jcenter()
		maven { url "https://repo.spring.io/snapshot" }
		maven { url "https://repo.spring.io/milestone" }
		maven { url "https://repo.spring.io/libs-milestone" }
		maven { url "https://plugins.gradle.org/m2/" }
	}
	dependencies {
		classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
		classpath("se.transmode.gradle:gradle-docker:1.2")
		classpath("com.moowork.gradle:gradle-node-plugin:0.12")
		classpath("com.moowork.gradle:gradle-gulp-plugin:0.12")
		classpath("gradle.plugin.com.gorylenko.gradle-git-properties:gradle-git-properties:1.4.6")
	}
}

apply plugin: 'java'
apply plugin: 'docker'
apply plugin: 'eclipse'
apply plugin: 'spring-boot'
apply plugin: 'war'
apply plugin: 'com.gorylenko.gradle-git-properties'
apply plugin: 'com.moowork.node'
apply plugin: 'com.moowork.gulp'

jar {
	baseName = 'orderprocessor'
	version = '0.0.1-SNAPSHOT'
}
sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
	mavenCentral()
	jcenter()
	maven { url "https://repo.spring.io/snapshot" }
	maven { url "https://repo.spring.io/milestone" }
	maven { url "https://repo.spring.io/libs-milestone" }
	maven { url "http://sndyuk.github.com/maven" }
}


dependencies {
	compile('org.springframework.boot:spring-boot-starter-amqp')
	compile('org.springframework.boot:spring-boot-starter-web')
	compile('org.springframework.boot:spring-boot-starter-data-jpa')
	compile('org.springframework.boot:spring-boot-starter-actuator')
	compile("org.springframework.boot:spring-boot-devtools")
	//compile('org.springframework.cloud:spring-cloud-starter-consul-all')
	compile('com.google.code.gson:gson:2.6.2')
	compile('org.projectlombok:lombok')
	compile('mysql:mysql-connector-java')
	compile('org.fluentd:fluent-logger:0.3.2')
    compile('com.sndyuk:logback-more-appenders:1.1.1')
	testCompile('org.springframework.boot:spring-boot-starter-test') 
}

dependencyManagement {
    imports { mavenBom "org.springframework.cloud:spring-cloud-consul-dependencies:1.0.0.RC1" }
}

eclipse {
	classpath {
		 containers.remove('org.eclipse.jdt.launching.JRE_CONTAINER')
		 containers 'org.eclipse.jdt.launching.JRE_CONTAINER/org.eclipse.jdt.internal.debug.ui.launcher.StandardVMType/JavaSE-1.8'
	}
}

task wrapper(type: Wrapper) {
	gradleVersion = '2.9'
}

war {
    rootSpec.exclude("**/tomcat-*.jar")
}

springBoot  {
    buildInfo()
    executable = true
}

bootRun {
    addResources = true
}

test {
    include '**/*UnitTest*'
    include '**/*IntTest*'
    ignoreFailures true
    reports.html.enabled = false
}

group = 'sdd330'

docker {
    baseImage "frolvlad/alpine-oraclejdk8:slim"
    maintainer "yang.leijun@gmail.com"
}

task appDocker(type: Docker, dependsOn: build) {
  push = true
  applicationName = jar.baseName
  dockerfile = file('src/main/docker/Dockerfile')
  doFirst {
    copy {
      from jar
      into stageDir
    }
  }
}

node {
	version = '6.1.0'
	npmVersion = '3.9.0'
	download = true
	workDir = file("${project.projectDir}/src/main/web/node")
	nodeModulesDir = file("${project.projectDir}/src/main/web")
}

gulp {
	workDir = file("${project.projectDir}/src/main/web")
	colors = true
	bufferOutput = false
}

// build front-end before making jar
processResources.dependsOn npmInstall
npmInstall.finalizedBy gulp_build